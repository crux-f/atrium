# Summon 1 Forgotten Heretic
# Shouldn't exist. Looking at them causes Nausea. Otherwise just footsoldiers.
#
summon drowned ~ ~ ~ {Silent:1b,DeathLootTable:"atrium:entities/oblivion_sect/forgotten_heretic",PersistenceRequired:1b,CanPickUpLoot:1b,Health:35f,Tags:["atrium_forgotten_heretic","atrium_trigger_on_eye_contact"],CustomName:'{"text":"Forgotten Heretic","color":"yellow","italic":false}',ArmorItems:[{id:"minecraft:air",Count:1b},{id:"minecraft:air",Count:1b},{id:"minecraft:air",Count:1b},{id:"minecraft:player_head",Count:1b,tag:{display:{Name:'{"text":"Ancient Symbol","color":"gray","italic":false}',Lore:['{"text":"The Nameless","color":"dark_blue","italic":false}']},atrium_nameless_symbol:1b,SkullOwner:{Id:[I;-544299348,1558596997,-2068327581,-1929284218],Properties:{textures:[{Value:"eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZWI0YzYwNGJjYTU1MzIyNDA0MTEyOGNhZjEyZTU2NjA0NTZmNGZmN2JjNjJjZjdjNWViODYzYjhjZWFmOTRlIn19fQ=="}]}}}}],ArmorDropChances:[0.250F,0.250F,0.250F,0.100F],ActiveEffects:[{Id:33,Amplifier:0b,Duration:999999999,ShowParticles:1b}],Attributes:[{Name:"generic.max_health",Base:35},{Name:"generic.follow_range",Base:60}]}
#
# Sometimes have gear
execute as @e[tag=atrium_forgotten_heretic,distance=..1] if predicate atrium:percentage_chances/0.35_p run item replace entity @s armor.chest with minecraft:golden_chestplate{Damage:80}
execute as @e[tag=atrium_forgotten_heretic,distance=..1] if predicate atrium:percentage_chances/0.35_p run item replace entity @s armor.legs with minecraft:golden_leggings{Damage:70}
execute as @e[tag=atrium_forgotten_heretic,distance=..1] if predicate atrium:percentage_chances/0.35_p run item replace entity @s armor.feet with minecraft:golden_boots{Damage:60}
execute as @e[tag=atrium_forgotten_heretic,distance=..1] if predicate atrium:percentage_chances/0.50_p run item replace entity @s weapon.mainhand with minecraft:iron_sword{display:{Name:'{"text":"Ruined Blade","italic":false}'},Damage:230,CustomModelData:333086,atrium_ruined_blade:1b,AttributeModifiers:[{AttributeName:"generic.attack_damage",Name:"generic.attack_damage",Amount:2,Operation:0,UUID:[I;624984712,1200570591,-1448101242,1012217807],Slot:"mainhand"}]}
execute as @e[tag=atrium_forgotten_heretic,distance=..1] if predicate atrium:percentage_chances/0.80_p run item modify entity @s weapon.mainhand atrium_events:ruined_blade_enchantments